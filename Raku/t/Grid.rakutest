#!/usr/bin/env raku

use AOC::Geometry;
use AOC::Grid;
use Test;

my $NUM_TESTS := 24;
plan $NUM_TESTS;

# Call testing functions
test_grid();

done-testing;

class GridObject does GridGlyph {
	has Int $.id;
	has Str $.type;
	has Int $.hp;
	
	method glyph(--> Str) {
		return $.type.substr(0, 1);
	}
}

sub test_grid() {
	say "\nTesting Grid";
	# g2_make g2_get_default g2_get_rule
	my $g2d = Grid.new(default => '.', rule => AdjacencyRule::ROOK);
	ok $g2d.default eq '.' or diag "Getting default";
	ok $g2d.rule eq AdjacencyRule::ROOK or diag "Getting rule";
	ok $g2d.extent().is_empty or diag 'Check initial extent is empty';

	my @coords = (Coord.new(x=>1,y=>1), Coord.new(x=>2,y=>2),
				  Coord.new(x=>3,y=>3), Coord.new(x=>4,y=>4),
				  Coord.new(x=>1,y=>4), Coord.new(x=>2,y=>4),
				  Coord.new(x=>3,y=>4));

	# set
	$g2d.set(@coords[0], 'A');
	$g2d.set(@coords[1], 'B');
	$g2d.set(@coords[3], 'D');

	# get
	ok $g2d.get(@coords[0]) eq 'A' or diag "Testing g2_get";
	ok $g2d.get(@coords[1]) eq 'B' or diag "Testing g2_get";
	ok $g2d.get(@coords[2]) eq $g2d.default or diag "Testing g2_get";
	ok $g2d.get(@coords[3]) eq 'D' or diag "Testing g2_get";
	
	# Non-string values
	my $elf = GridObject.new(id => 1, type => 'Elf', hp => 100);
	my %goblin = 'id' => 2, 'type' => 'Goblin', 'hp' => 95, 'glyph' => 'G';
	my @santa = ['S', 3, 'Santa', 110];
	
	$g2d.set(@coords[4], $elf);
	$g2d.set(@coords[5], %goblin);
	$g2d.set(@coords[6], @santa);
	
	ok $g2d.get_glyph(@coords[4]) eq 'E' or diag "Elf did not return the correct glyph.";
	ok $g2d.get_glyph(@coords[5]) eq 'G' or diag "Goblin did not return the correct glyph.";
	ok $g2d.get_glyph(@coords[6]) eq 'S' or diag "Santa did not return the correct glyph.";
	
	# extent
	my $e = $g2d.extent;
	ok $e.nw eqv Coord.from_ints(1,1), 'Testing extent min' or diag "Extent incorrect";
	ok $e.se eqv Coord.from_ints(4,4), 'Testing extent max' or diag "Extent incorrect";


	# coords coords_with_value
	my @all = $g2d.coords;
	ok @all.elems == 6, 'Checking returned coords count' or diag 'Incorrect number of coords.';
	my @matching = $g2d.coords('B');
	ok @matching.elems == 1, 'Checking returned coords with value count' or diag 'Incorrect number of coords.';
	ok @matching[0] eqv @coords[1], 'Checking returned coord' or diag 'Wrong coord with B';


	# histogram
	$g2d.set(@coords[2], 'B');
	my %hist = $g2d.histogram;
	#dd %hist;
	ok %hist{'A'} == 1 && %hist{'B'} == 2 && (%hist{'.'}:exists == False), 'Checking histogram' or diag "Failed";
	%hist = $g2d.histogram(True);
	#dd %hist;
	ok %hist{'A'} == 1 && %hist{'B'} == 2 && %hist{'.'} == 9, 'Checking histogram including unset' or diag "failed";

	# neighbors
	my @n = $g2d.neighbors(@coords[1]);
	ok @n[0] eqv Coord.from_ints(2,1) && @n[1] eqv Coord.from_ints(3,2) &&
		@n[2] eqv Coord.from_ints(2,3) && @n[3] eqv Coord.from_ints(1,2), 'Checking rook neighbours'
		or diag "Rook neighbors incorrect.";

	
	# print
	$g2d.print();
	my $grid_str = $g2d.sprint();
	my $expected = "A . . . \n. B . . \n. . B . \nE G S D \n";
	ok $grid_str eq $expected, 'Checking grid to string';

	$grid_str = $g2d.sprint(invert_y => True);
	$expected = "E G S D \n. . B . \n. B . . \nA . . . \n";
	ok $grid_str eq $expected, 'Checking inverted grid to string';

	my %markers = (Coord.from_ints(4,1) => '*');
	$grid_str = $g2d.sprint(markers => %markers);
	$expected = "A . . * \n. B . . \n. . B . \nE G S D \n";
	ok $grid_str eq $expected, 'Checking grid with markers to string';

	# clear
	$g2d.clear(@coords[2]);
	ok $g2d.get(@coords[2]) eq '.', 'Checking grid clearing';
	my $e_original = $g2d.extent();
	$g2d.set(Coord.from_ints(100, 100), 'X');
	ok $g2d.extent.se eqv Coord.from_ints(100,100), "Extent after expand is big";
	$g2d.clear(Coord.from_ints(100, 100), reset_extent => True);
	ok $g2d.extent eqv $e_original, "Extent after clear is small";

}